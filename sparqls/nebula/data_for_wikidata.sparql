PREFIX schema: <http://schema.org/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
select * where {
    {
        select ?organization ?name(group_concat(DISTINCT ?knowsAboutWdid) as ?knowsAbout) ?wasDerivedFrom  ?aduri  ?country  ?addressLocality ?postalCode ?bn   ?wduri where {
            graph <http://kg.artsdata.ca/culture-creates/artsdata-planet-ipaa/ipaa-entities> {
                ?organization a schema:Organization ;
                   prov:wasDerivedFrom ?wasDerivedFrom ;
                   schema:name ?name .
                OPTIONAL {
                    ?organization schema:knowsAbout/schema:sameAs ?knowsAboutWdid .
                }
                OPTIONAL {
                    ?organization schema:address/schema:addressCountry ?country .
                }
                OPTIONAL {
                    ?organization schema:address/schema:addressLocality ?addressLocality .
                }
                OPTIONAL {
                    ?organization schema:address/schema:postalCode ?postalCode .
                }
                OPTIONAL {
                    ?organization wdt:P8880 ?bn .
                }
            }
            OPTIONAL {
                ?organization  schema:sameAs ?aduri_indirect .
                filter(strstarts(str(?aduri_indirect),"http://kg.artsdata.ca/resource/K"))
            }
            OPTIONAL {
                ?aduri_direct  schema:sameAs ?organization  .
                filter(strstarts(str(?aduri_direct),"http://kg.artsdata.ca/resource/K"))
            }
            bind(coalesce(?aduri_direct, ?aduri_indirect)   as ?aduri)

            OPTIONAL {
                ?organization  schema:sameAs ?wduri_indirect .
                filter(strstarts(str(?wduri_indirect),"http://www.wikidata.org/entity/Q"))
            }
            OPTIONAL {
                ?organization   schema:sameAs ?aduri_direct  .
                filter(strstarts(str(?aduri_direct),"http://kg.artsdata.ca/resource/K"))
                ?aduri_direct   schema:sameAs ?wduri_direct  .
                filter(strstarts(str(?wduri_direct),"http://www.wikidata.org/entity/Q"))
            }
            bind(coalesce(?wduri_direct, ?wduri_indirect)   as ?wduri) 
            filter(bound(?wduri))
        } group by ?organization ?name ?wasDerivedFrom ?aduri ?country ?addressLocality ?postalCode ?bn  ?wduri
    }
    OPTIONAL {
        SERVICE <https://query.wikidata.org/sparql> {
            SELECT ?wduri ?wduriLabel ?wdLatestStartDate ?retrievedDate ?referenceUrl WHERE {
                ?wduri wdt:P463 wd:Q110938829;
                       p:P463 ?statement.
                ?statement ps:P463 wd:Q110938829.
                OPTIONAL {
                    ?statement prov:wasDerivedFrom ?ref.
                    ?ref pr:P248 wd:Q113164737.
                    OPTIONAL {
                        ?ref pr:P813 ?retrievedDate.
                    }
                    OPTIONAL {
                        ?ref pr:P854 ?referenceUrl.
                    }
                }
                OPTIONAL {
                    ?statement pq:P8555 ?wdLatestStartDate.
                }
                SERVICE wikibase:label {
                    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en".
                }
            }
        } 
    }
}